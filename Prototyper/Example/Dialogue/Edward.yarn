title: Edward_Intro
---
<<if visited("Edward_Intro") is false>>
    Edward: Hi there.
    Player: Hey. Umm..
    Edward: What's wrong? Never seen a giant in a barn before?
    Player: Once. But that's not it.
<<endif>>
Edward: What's up?
<<if $package is true>>
    <<if $delivered is false>>
        Player: I have a package for you. From Alice.
        Edward: Oh brilliant!
        // Option "Is this an important package for you? " moved to the end of this node
        // Option "Here's your package. " moved to the end of this node
    <<endif>>
<<else>>
    <<if $delivered is false>>
        Player: Nothing, really.
        Player: Nice to meet you, though.
    <<else>>
        <<jump Edward_Thanks>>
    <<endif>>
<<endif>>
-> Is this an important package for you?  <<if ($package is true) && ($delivered is false)>>
    <<jump Edward_ExplainPackage>>
-> Here's your package.  <<if ($package is true) && ($delivered is false)>>
    <<jump Edward_DeliverPackage>>
===

title: Edward_ExplainPackage
---
    Edward: I guess. It's my books.
    Edward: About how to live alone.
    Edward: In a barn.
    Edward: I can't wait!
===

title: Edward_DeliverPackage
---
    <<set $package to false>>
    <<set $delivered to true>>
    <<call message("You give the package to Edward.")>>
    <<jump Edward_ReceivePackage>>
===

title: Edward_ReceivePackage
---
    Edward: Finally, my books have arrived!
    <<jump Edward_Thanks>>
===

title: Edward_Thanks
---
    Edward: Thanks for the package!
    Edward: You should go see Alice now.
===
