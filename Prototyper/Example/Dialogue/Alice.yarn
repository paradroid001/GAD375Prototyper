title: Alice_Hello
---
<<if visited("Alice_Hello") is false>>
    <<set $package to false>>
    <<set $delivered to false>>
    Alice: Hello there, I'm Alice!
    Player: Hi.
    Alice: You're probably wondering what you're supposed to do!
    Player: I sure am!
<<else>>
    Alice: Hi again!
<<endif>>

<<if $delivered is false>>
    <<jump Alice_Quest>>
<<else>>
    <<jump Alice_Congratulations>>
<<endif>>
===

title: Alice_Quest
---
<<if $package is false>>
    Alice: What I need you to do is take this package to Edward.
    <<set $package to true>>
    <<call message("You recieve the package.")>>
    // Option "Who is Edward? " moved to the end of this node
<<else>>
    Alice: Have you delivered my package yet?
    // Option "How do I find Edward, again? " moved to the end of this node
    // Option "What do I do when I'm finished? " moved to the end of this node
<<endif>>
// Option "I understand, thanks. " moved to the end of this node
-> Who is Edward?  <<if ($package is false)>>
    <<jump Alice_ExplainEdward>>
-> How do I find Edward, again?  <<if !($package is false)>>
    <<jump Alice_LocationEdward>>
-> What do I do when I'm finished?  <<if !($package is false)>>
    <<jump Alice_ExplainEnd>>
-> I understand, thanks. 
    <<jump Alice_Exit>>
===

title: Alice_ExplainEdward
---
    Alice: Edward is a giant who lives in a barn!
    Alice: He really needs that package!
    <<jump Alice_Quest>>
===

title: Alice_LocationEdward
---
    Alice: Well, Edward is in the barn. The barn is red.
    Alice: It's kind of big because Edward is a giant!
    Alice: It's through the forest and over the river!
    Player: Great, that's incredibly specific, thank you.
    Alice: Welcome!
    <<jump Alice_Quest>>
===

title: Alice_ExplainEnd
---
    Alice: Just come back and talk to me!
    <<jump Alice_Quest>>
===

title: Alice_Exit
---
    Alice: Good luck!
===

title: Alice_Congratulations
---
    Alice: You did it! Your quest is over!
    Alice: Congratulations, you win!
    Player: Hooray.
===
